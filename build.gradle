plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'com.github.romanqed'
archivesBaseName = 'jeflect'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    api group: 'org.ow2.asm', name: 'asm', version: '9.3'
    api group: 'org.ow2.asm', name: 'asm-commons', version: '9.3'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set('jeflect')
                description.set('A set of utilities designed to interact with reflection and speed up it.')
                url.set('https://github.com/RomanQed/jeflect')
                organization {
                    name.set('com.github.romanqed')
                    url.set('https://github.com/RomanQed/')
                }
                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/RomanQed/jeflect/issues')
                }
                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://github.com/RomanQed/jeflect/blob/main/LICENSE')
                    }
                }
                scm {
                    url.set('https://github.com/RomanQed/jeflect')
                    connection.set('scm:https://github.com/RomanQed/jeflect.git')
                    developerConnection.set('scm:https://github.com/RomanQed/jeflect.git')
                }
                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}
